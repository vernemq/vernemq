#!/usr/bin/env escript
%%! +fnu
%% -*- mode: erlang;erlang-indent-level: 4;indent-tabs-mode: nil -*-
%% ex: ft=erlang ts=4 sw=4 et
%% -------------------------------------------------------------------
%%
%% nodetool: Helper Script for interacting with live nodes
%%
%% -------------------------------------------------------------------

%% installed by node_package (github.com/basho/node_package)

main(Args) ->
    io:setopts([{encoding, utf8}]),
    ok = start_epmd(),
    %% Extract the args
    {RestArgs, TargetNode} = process_args(Args, [], undefined),

    %% Extract the RPC timeout from process dictionary, if it's defined
    RpcTimeout = case erlang:get(rpctimeout) of
                     undefined ->
                         60000;
                     Value ->
                         Value
                 end,

    %% process_args() has side-effects (e.g. when processing "-name"),
    %% so take care of app-starting business first.
    [application:start(App) || App <- [crypto, public_key, ssl]],

    %% any commands that don't need a running node
    case RestArgs of
        ["chkconfig", File] ->
            chkconfig(File);
        ["chkconfig", "-config", File|_] ->
            chkconfig(File);
        _ ->
            ok
    end,

    %% See if the node is currently running  -- if it's not, we'll bail
    case {net_kernel:hidden_connect_node(TargetNode), net_adm:ping(TargetNode)} of
        {true, pong} ->
            ok;
        {false, pong} ->
            io:format(standard_error, "failed to connect to node ~p .\n", [TargetNode]),
            halt(1);
        {_, pang} ->
            io:format(standard_error, "Node ~p not responding to pings.\n", [TargetNode]),
            halt(1)
    end,

    case RestArgs of
        ["getpid"] ->
            io:format("~p\n", [list_to_integer(rpc:call(TargetNode, os, getpid, []))]);
        ["ping"] ->
            %% If we got this far, the node already responded to a ping, so just dump
            %% a "pong"
            io:format("pong\n");
        ["stop"] ->
            io:format("~p\n", [rpc:call(TargetNode, erlang, apply,
                                        [fun() ->
                                                 catch error_logger:info_msg("Administrative stop\n"),
                                                 init:stop()
                                         end, []], RpcTimeout)]);
        ["restart"] ->
            io:format("~p\n", [rpc:call(TargetNode, erlang, apply,
                                        [fun() ->
                                                 catch error_logger:info_msg("Administrative restart\n"),
                                                 init:restart()
                                         end, []], RpcTimeout)]);
        ["reboot"] ->
            io:format("~p\n", [rpc:call(TargetNode, erlang, apply,
                                        [fun() ->
                                                 catch error_logger:info_msg("Administrative reboot\n"),
                                                 init:reboot()
                                         end, []], RpcTimeout)]);
        ["rpc", Module, Function | RpcArgs] ->
            case rpc:call(TargetNode, list_to_atom(Module), list_to_atom(Function),
                          [RpcArgs], RpcTimeout) of
                ok ->
                    ok;
                {badrpc, Reason} ->
                    io:format(standard_error, "RPC to ~p failed: ~p\n", [TargetNode, Reason]),
                    halt(1);
                {error, Code} when is_integer(Code) ->
                    halt(Code);
                {error, Code, Reason} when is_integer(Code) ->
                    io:format(standard_error, "ERROR: ~p\n", [Reason]),
                    halt(Code);
                _ ->
                    halt(1)
            end;
        ["rpc_infinity", Module, Function | RpcArgs] ->
            case rpc:call(TargetNode, list_to_atom(Module), list_to_atom(Function), [RpcArgs], infinity) of
                ok ->
                    ok;
                {badrpc, Reason} ->
                    io:format(standard_error, "RPC to ~p failed: ~p\n", [TargetNode, Reason]),
                    halt(1);
                {error, Code} when is_integer(Code) ->
                    halt(Code);
                {error, Code, Reason} when is_integer(Code) ->
                    io:format(standard_error, "ERROR: ~p\n", [Reason]),
                    halt(Code);
                _ ->
                    halt(1)
            end;
        ["rpcterms", Module, Function, ArgsAsString] ->
            case rpc:call(TargetNode, list_to_atom(Module), list_to_atom(Function),
                          consult(ArgsAsString), RpcTimeout) of
                {badrpc, Reason} ->
                    io:format(standard_error, "RPC to ~p failed: ~p\n", [TargetNode, Reason]),
                    halt(1);
                {error, Code} when is_integer(Code) ->
                    halt(Code);
                {error, Code, Reason} when is_integer(Code) ->
                    io:format(standard_error, "ERROR: ~p\n", [Reason]),
                    halt(Code);
                Other ->
                    io:format("~p\n", [Other])
            end;
        Other ->
            io:format("Other: ~p\n", [Other]),
            io:format("Usage: nodetool {ping|stop|restart|reboot|chkconfig}\n")
    end,
    net_kernel:stop().

process_args([], Acc, TargetNode) ->
    {lists:reverse(Acc), TargetNode};
process_args(["-kernel", "net_ticktime", Value | Rest], Acc, TargetNode) ->
    application:set_env(kernel, net_ticktime, list_to_integer(Value)),
    process_args(Rest, Acc, TargetNode);
process_args(["-setcookie", Cookie | Rest], Acc, TargetNode) ->
    erlang:set_cookie(node(), list_to_atom(Cookie)),
    process_args(Rest, Acc, TargetNode);
process_args(["-name", TargetName | Rest], Acc, _) ->
    ThisNode = append_node_suffix(TargetName, "_maint_"),
    {ok, _} = net_kernel:start([ThisNode, longnames]),
    process_args(Rest, Acc, nodename(TargetName));
process_args(["-sname", TargetName | Rest], Acc, _) ->
    ThisNode = append_node_suffix(TargetName, "_maint_"),
    {ok, _} = net_kernel:start([ThisNode, shortnames]),
    process_args(Rest, Acc, nodename(TargetName));
process_args(["-rpctimeout", TimeoutStr | Rest], Acc, TargetNode) ->
    Timeout = case TimeoutStr of
                  "infinity" -> infinity;
                  _          -> list_to_integer(TimeoutStr)
              end,
    erlang:put(rpctimeout, Timeout),
    process_args(Rest, Acc, TargetNode);
process_args([Arg | Rest], Acc, Opts) ->
    process_args(Rest, [Arg | Acc], Opts).


start_epmd() ->
    case os:getenv("NO_EPMD") of
      false ->
        [] = os:cmd(epmd_path() ++ " -daemon"),
        ok;
      _ ->
        ok
    end.

epmd_path() ->
    ErtsBinDir = filename:dirname(escript:script_name()),
    Name = "epmd",
    case os:find_executable(Name, ErtsBinDir) of
        false ->
            case os:find_executable(Name) of
                false ->
                    io:format("Could not find epmd.~n"),
                    halt(1);
                GlobalEpmd ->
                    GlobalEpmd
            end;
        Epmd ->
            Epmd
    end.


nodename(Name) ->
    case string:tokens(Name, "@") of
        [_Node, _Host] ->
            list_to_atom(Name);
        [Node] ->
            [_, Host] = string:tokens(atom_to_list(node()), "@"),
            list_to_atom(lists:concat([Node, "@", Host]))
    end.

append_node_suffix(Name, Suffix) ->
    Rnd = erlang:phash2({os:timestamp(),os:getpid()}, 1000),
    case string:tokens(Name, "@") of
        [Node, Host] ->
            list_to_atom(lists:concat([Node, Suffix, Rnd, "@", Host]));
        [Node] ->
            list_to_atom(lists:concat([Node, Suffix, Rnd]))
    end.

chkconfig(File) ->
    case file:consult(File) of
        {ok, _} ->
            io:format("ok\n"),
            halt(0);
        {error, {Line, Mod, Term}} ->
            io:format(standard_error, 
                      ["Error on line ", 
                       file:format_error({Line, Mod, Term}), "\n"], []),
            halt(1);
        {error, R} ->
            io:format(standard_error,
                      ["Error reading config file: ", 
                       file:format_error(R), "\n"], []),
            halt(1)
    end.


%%
%% Given a string or binary, parse it into a list of terms, ala file:consult/0
%%
consult(Str) when is_list(Str) ->
    consult([], Str, []);
consult(Bin) when is_binary(Bin)->
    consult([], binary_to_list(Bin), []).

consult(Cont, Str, Acc) ->
    case erl_scan:tokens(Cont, Str, 0) of
        {done, Result, Remaining} ->
            case Result of
                {ok, Tokens, _} ->
                    {ok, Term} = erl_parse:parse_term(Tokens),
                    consult([], Remaining, [Term | Acc]);
                {eof, _Other} ->
                    lists:reverse(Acc);
                {error, Info, _} ->
                    {error, Info}
            end;
        {more, Cont1} ->
            consult(Cont1, eof, Acc)
    end.
